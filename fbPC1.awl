//----------------------------------------------------------------------------//
//            Copyright 2018 Rieckermann Engineering Operations               //
//----------------------------------------------------------------------------//
// Class:       fbPC1                                                         //
// Description:                                                               //
// Vessel ON/OFF pressure control loop with output valve selection.           //
//----------------------------------------------------------------------------//
// Revision history:                                                          //
// Rev By               Date        CC        Note                            //
// 2.0 David Paspa      30-Jan-2018 NA        Reboot for S7-1500.             //
// 0.6 Gerald Kontriner 09-Oct-2009 CC-09/016 MON_MISMATCH Logic.             //
// 0.5 Khairul Basar    16-Sep-2009 CC-09/016 Duplicate Input Parameters      //
//                                            deleted auto/manual both now    //
//                                            using same parameters,Interface //
//                                            FC used for Bumpless changeover,//
//                                            IL close valves.                //
// 0.4 Khairul Basar    15-Sep-2009 CC-09/016 Ref3.                           //
// 0.3 Khairul Basar    09-Sep-2009 CC-09/016 Ref2.                           //
// 0.2 Khairul Basar    01-Sep-2009 CC-09/016 Ref1.                           //
// 0.1 Khairul Basar    03-Aug-2009           Initial design.                 //
// Ref3:                                                                      //
// 1.If OPEN=3 state is selected, then selected valve should open until       //
// commanded to close.                                                        //
// 2. When CLOSE=1 state is selected, then all the valves must be closed.     //
// 3.When P_CTRL=2 state is selected, then Selected valve should open or close//
// based on FS Requirement F1, F2 & F3.                                       //
//                                                                            //
// In DB1 these above 3-states are added, P_CTRL data type changed from Bool  //
// to Int.                                                                    //
// (Note: This PC1 is controlled by EMC1, which says PC1 to operate in all    //
// these 3-states, whereas previously there was only two states defined       //
// ENABLED & DISABLED)                                                        //
//                                                                            //
// Ref2:                                                                      //
// Manual and Auto mode is programed.Manual & auto separate Setpoint, Gas     //
// select, & P_CTRL variable created and programmed accordingly.              //
//                                                                            //
// Ref1:                                                                      //
// Nw5-:db_low= SETPOINT-DEADBAND_L (not _H, corrected).                      //
// Nw6: Comparator changed from less_than_or_Equal to Less- than.  Now Valve  //
// will open when value Measured is less than db_low. (P_CTRL mode)           //
// Nw4-:db_high= SETPOINT+DEADBAND_H.                                         //
// Nw6: Comparator changed from greater_than_or_Equal to Greater-than. Now    //
// Valve will close when value Measured greater than db_high.(P_CTRL mode)    //
// Output command OUT_Qx is BOOL is changed to CMD_Qx of type INT which is the//
// normal requirement to which the PC1 is giving command. Ref. Nw8-11.        //
// PC1 correct device state PC1_DEV_STATE assigned where corrected value of   //
// ENABLED=1, DISABLED=2.                                                     //
// Initial Value of MEASURE is changed to 1.0                                 //
//----------------------------------------------------------------------------//
FUNCTION_BLOCK "fbPC1"
TITLE = PC1 : Vessel ON/OFF pressure control loop with output valve sel
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : REO
NAME : fbPC1
VERSION : 2.0

VAR_INPUT
    PI1_PV : Real;           // Pressure measured process value (after scaling)
    PI1_INTERLOCK : Bool;    // Pressure indicator interlock
    DB_L : Real;             // Deadband LOW differential
    DB_H : Real;             // Deadband HIGH differential
    _SETPOINT : Real;        // Pressure setpoint
    _MEDIUM_GAS : Int;       // Select medium valve to be operated
END_VAR

VAR_OUTPUT
    VLV_DRAIN : Int;         // Output command to DRAIN VALVE
    VLV_HPCA : Int;          // Output command to HPCA VALVE
    VLV_LPCA : Int;          // Output command to LPCA VALVE
    VLV_VACUUM : Int;        // Output command to VACUUM VALVE
    VLV_VENT : Int;          // Output command to VENT VALVE
END_VAR

VAR
    mc : "udtModeCommand";   // Block mode and command
END_VAR

VAR_TEMP
    cmdDISABLE : Bool;
    cmdPRESSURE : Bool;
    cmdVALVE : Bool;
    selectDRAIN : Bool;
    selectHPCA : Bool;
    selectLPCA : Bool;
    selectVACUUM : Bool;
    selectVENT : Bool;
    dbHigh : Real;   // Deadband pressure high
    dbLow : Real;   // Deadband pressure low
    tDO : Bool;
END_VAR

BEGIN
NETWORK
TITLE = Call Mode and Command handling block
    //------------------------------------------------------------------------//
    // COMMAND PROCESSOR   COMMAND PROCESSOR   COMMAND PROCESSOR   COMMAND    //
    //                                                                        //
    // Process the standard block functions for command, mode, interlock and  //
    // state:                                                                 //
    //------------------------------------------------------------------------//
    CALL "fcModeCmd"
    (   mc                   := #mc
    );

NETWORK
TITLE = Command is DISABLE
    //------------------------------------------------------------------------//
    // COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND    //
    // COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND    //
    //                                                                        //
    // CMD is DISABLE:                                                        //
    //------------------------------------------------------------------------//
    L #mc.CMD;
    L "dbCONST".BLK.PC1.CMD.DISABLE;
    ==I;
    = #cmdDISABLE;

NETWORK
TITLE = Command is PRESSURE control
    //------------------------------------------------------------------------//
    // CMD is PRESSURE control and no fault condition:                        //
    //------------------------------------------------------------------------//
    AN mc.modeOOS;
    AN mc.INTERLOCK;
    AN PI1_INTERLOCK;
    L #mc.CMD;
    L "dbCONST".BLK.PC1.CMD.PRESSURE;
    ==I;
    = #cmdPRESSURE;

NETWORK
TITLE = Command is VALVE open
    //------------------------------------------------------------------------//
    // CMD is VALVE open and no fault condition:                              //
    //------------------------------------------------------------------------//
    AN mc.modeOOS;
    AN mc.INTERLOCK;
    L #mc.CMD;
    L "dbCONST".BLK.PC1.CMD.VALVE;
    ==I;
    = #cmdVALVE;

NETWORK
TITLE = Calculate high pressure valve close limit in PRESSURE
    //------------------------------------------------------------------------//
    // INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS  //
    // INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS  //
    //                                                                        //
    // Add deadband high to setpoint to calculate the value when the selected //
    // valve should close in PRESSURE control:                                //
    //------------------------------------------------------------------------//
    A #cmdPRESSURE;
    JCN _a;
    L #_SETPOINT;
    L #DB_H;
    +R;
    T #dbHigh;
_a:   NOP 0;

NETWORK
TITLE = Calculate low pressure valve open limit in PRESSURE
    //------------------------------------------------------------------------//
    // Subtract deadband low from setpoint to calculate the value when the    //
    // selected valve should open in PRESSURE control:                        //
    //------------------------------------------------------------------------//
    A #cmdPRESSURE;
    JCN _b;
    L #_SETPOINT;
    L #DB_L;
    -R;
    T #dbLow;
_b:   NOP 0;

NETWORK
TITLE = Selected valve is DRAIN
    //------------------------------------------------------------------------//
    // Selected GAS valve is DRAIN:                                           //
    //------------------------------------------------------------------------//
    L #_MEDIUM_GAS;
    L "dbCONST".SEL.GAS.DRAIN;
    ==I;
    = #selectDRAIN;

NETWORK
TITLE = Selected valve is HPCA
    //------------------------------------------------------------------------//
    // Selected GAS valve is HPCA:                                            //
    //------------------------------------------------------------------------//
    L #_MEDIUM_GAS;
    L "dbCONST".SEL.GAS.HPCA;
    ==I;
    = #selectHPCA;

NETWORK
TITLE = Selected valve is LPCA
    //------------------------------------------------------------------------//
    // Selected GAS valve is LPCA:                                            //
    //------------------------------------------------------------------------//
    L #_MEDIUM_GAS;
    L "dbCONST".SEL.GAS.LPCA;
    ==I;
    = #selectLPCA;

NETWORK
TITLE = Selected valve is VACUUM
    //------------------------------------------------------------------------//
    // Selected GAS valve is VACUUM:                                          //
    //------------------------------------------------------------------------//
    L #_MEDIUM_GAS;
    L "dbCONST".SEL.GAS.VACUUM;
    ==I;
    = #selectVACUUM;

NETWORK
TITLE = Selected valve is VENT
    //------------------------------------------------------------------------//
    // Selected GAS valve is VENT:                                            //
    //------------------------------------------------------------------------//
    L #_MEDIUM_GAS;
    L "dbCONST".SEL.GAS.VENT;
    ==I;
    = #selectVENT;

NETWORK
TITLE = Check if selected vclve should be OPENED
    //------------------------------------------------------------------------//
    // OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS    //
    // OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS    //
    //                                                                        //
    // Check if selected valve should be OPENED if PRESSURE control and       //
    // pressure measured process PV is less than the setpoint minus the       //
    // low deadband or if VALVE open command:                                 //
    //------------------------------------------------------------------------//
    O #cmdVALVE;
    O(;
    A #cmdPRESSURE;
    A(;
    L #PI1_PV;
    L #dbLow;
    <R;
    );
    );
    JCN _c;
    S #tDO;
_c:   NOP 0;

NETWORK
TITLE = Check if selected valve should be CLOSED
    //------------------------------------------------------------------------//
    // Check if selected valve should be CLOSED if PRESSURE control and       //
    // pressure measured process PV is greater than the setpoint plus the     //
    // high deadband or if VALVE open command:                                //
    //------------------------------------------------------------------------//
    O #cmdDISABLE;
    O(;
    A #cmdPRESSURE;
    A(;
    L #PI1_PV;
    L #dbHigh;
    >R;
    );
    );
    JCN _d;
    R #tDO;
_d:   NOP 0;

NETWORK
TITLE = Open DRAIN valve if required
    //------------------------------------------------------------------------//
    // OPEN VALVE   OPEN VALVE   OPEN VALVE   OPEN VALVE   OPEN VALVE   OPEN  //
    //                                                                        //
    // If PRESSURE or VALVE and DRAIN selected then OPEN it:                  //
    //------------------------------------------------------------------------//
    A #tDO;
    A #selectDRAIN;
    JCN _e;
    L "dbCONST".BLK.POSx.CMD.OPEN;
    T #VLV_DRAIN;
_e:   NOP 0;

NETWORK
TITLE = Open HPCA valve if required
    //------------------------------------------------------------------------//
    // If PRESSURE or VALVE and HPCA selected then OPEN it:                   //
    //------------------------------------------------------------------------//
    A #tDO;
    A #selectHPCA;
    JCN _f;
    L "dbCONST".BLK.POSx.CMD.OPEN;
    T #VLV_HPCA;
_f:   NOP 0;

NETWORK
TITLE = Open LPCA valve if required
    //------------------------------------------------------------------------//
    // If PRESSURE or VALVE and LPCA selected then OPEN it:                   //
    //------------------------------------------------------------------------//
    A #tDO;
    A #selectLPCA;
    JCN _g;
    L "dbCONST".BLK.POSx.CMD.OPEN;
    T #VLV_LPCA;
_g:   NOP 0;

NETWORK
TITLE = Open VACUUM valve if required
    //------------------------------------------------------------------------//
    // If PRESSURE or VALVE and VACUUM selected then OPEN it:                 //
    //------------------------------------------------------------------------//
    A #tDO;
    A #selectVACUUM;
    JCN _h;
    L "dbCONST".BLK.POSx.CMD.OPEN;
    T #VLV_VACUUM;
_h:   NOP 0;

NETWORK
TITLE = Open VENT valve if required
    //------------------------------------------------------------------------//
    // If PRESSURE or VALVE and VENT selected then OPEN it:                   //
    //------------------------------------------------------------------------//
    A #tDO;
    A #selectVENT;
    JCN _i;
    L "dbCONST".BLK.POSx.CMD.OPEN;
    T #VLV_VENT;
_i:   NOP 0;

NETWORK
TITLE = Close DRAIN valve if required
    //------------------------------------------------------------------------//
    // CLOSE VALVE   CLOSE VALVE   CLOSE VALVE   CLOSE VALVE   CLOSE VALVE    //
    //                                                                        //
    // If DRAIN valve to be closed then CLOSE it:                             //
    //------------------------------------------------------------------------//
    ON #tDO;
    ON #selectDRAIN;
    JCN _j;
    L "dbCONST".BLK.POSx.CMD.CLOSE;
    T #VLV_DRAIN;
_j:   NOP 0;

NETWORK
TITLE = Close HPCA valve if required
    //------------------------------------------------------------------------//
    // If HPCA valve to be closed then CLOSE it:                              //
    //------------------------------------------------------------------------//
    ON #tDO;
    ON #selectHPCA;
    JCN _k;
    L "dbCONST".BLK.POSx.CMD.CLOSE;
    T #VLV_HPCA;
_k:   NOP 0;

NETWORK
TITLE = Close LPCA valve if required
    //------------------------------------------------------------------------//
    // If LPCA valve to be closed then CLOSE it:                              //
    //------------------------------------------------------------------------//
    ON #tDO;
    ON #selectLPCA;
    JCN _l;
    L "dbCONST".BLK.POSx.CMD.CLOSE;
    T #VLV_LPCA;
_l:   NOP 0;

NETWORK
TITLE = Close VACUUM valve if required
    //------------------------------------------------------------------------//
    // If VACUUM valve to be closed then CLOSE it:                            //
    //------------------------------------------------------------------------//
    ON #tDO;
    ON #selectVACUUM;
    JCN _m;
    L "dbCONST".BLK.POSx.CMD.CLOSE;
    T #VLV_VACUUM;
_m:   NOP 0;

NETWORK
TITLE = Close VENT valve if required
    //------------------------------------------------------------------------//
    // If VENT valve to be closed then CLOSE it:                              //
    //------------------------------------------------------------------------//
    ON #tDO;
    ON #selectVENT;
    JCN _n;
    L "dbCONST".BLK.POSx.CMD.CLOSE;
    T #VLV_VENT;
_n:   NOP 0;

NETWORK
TITLE = Set Device State to Disabled if commanded
    //------------------------------------------------------------------------//
    // STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE  //
    // STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE  //
    //                                                                        //
    // State is DISABLED if commanded to DISABLE:                             //
    //------------------------------------------------------------------------//
    A #cmdDISABLE;
    JCN _o;
    L "dbCONST".BLK.PC1.STATE.DISABLED;
    T #mc.STATE;
_o:   NOP 0;

NETWORK
TITLE = Set Device State to PRESSURE if commanded
    //------------------------------------------------------------------------//
    // State is PRESSURE if commanded to PRESSURE:                            //
    //------------------------------------------------------------------------//
    A #cmdPRESSURE;
    JCN _p;
    L "dbCONST".BLK.PC1.STATE.PRESSURE;
    T #mc.STATE;
_p:   NOP 0;

NETWORK
TITLE = Set Device State to Fault if field fault signal is on
    //------------------------------------------------------------------------//
    // State is VALVE if commanded to VALVE:                                  //
    //------------------------------------------------------------------------//
    A #cmdVALVE;
    JCN _q;
    L "dbCONST".BLK.PC1.STATE.VALVE;
    T #mc.STATE;
_q:   NOP 0;
END_FUNCTION_BLOCK
