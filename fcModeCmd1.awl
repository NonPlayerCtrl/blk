//----------------------------------------------------------------------------//
//            Copyright 2018 Rieckermann Engineering Operations               //
//----------------------------------------------------------------------------//
// Description: Generic Block Common Mode and Command handling function block.//
//----------------------------------------------------------------------------//
// Revision history:                                                          //
// Rev By               Date        CC        Note                            //
// 2.0 David Paspa      28-Feb-2018 NA        Reboot for S7-1500.             //
// 0.4 Khairul Basar    05-Oct-2009 NA        Substate change will issue STOP //
// 0.3 Khairul Basar    17-Sep-2009 NA        Instead of Auto/Man command,    //
//                                            now only single COMMAND.        //
// 0.2 Khairul Basar    20-Aug-2009 NA        Ref1.                           //
// 0.1 Khairul Basar    03-Aug-2009 NA        Initial design.                 //
// Ref1.                                                                      //
// Mode change:#tMode_Changed NO is removed from the parallel path of         //
// Substate changed for not to generate #tEdgeAutoStart pulse when mode is    //
// changed.                                                                   //
//----------------------------------------------------------------------------//
FUNCTION "fcMC" : Void
TITLE = General Mode, Command & Interlock handling block
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : REO
NAME : fcMC
VERSION : 2.0

VAR_IN_OUT
    mc : "udtModeCommand";   // Block mode and command
END_VAR

BEGIN
NETWORK
TITLE = Auto Mode on CPU startup
    //------------------------------------------------------------------------//
    // RESTART   RESTART   RESTART   RESTART   RESTART   RESTART   RESTART    //
    // RESTART   RESTART   RESTART   RESTART   RESTART   RESTART   RESTART    //
    //                                                                        //
    // Set the mode to Auto if CPU has restarted:                             //
    //------------------------------------------------------------------------//
    A "flagFirstScanRestart";
    JCN _a;
    L "dbCONST".BLK.MODE.AUTO;
    T #mc.MODE;
_a:   NOP 0;

NETWORK
TITLE = Mode Auto
    //------------------------------------------------------------------------//
    // MODE   MODE   MODE   MODE   MODE   MODE   MODE   MODE   MODE   MODE    //
    // MODE   MODE   MODE   MODE   MODE   MODE   MODE   MODE   MODE   MODE    //
    //                                                                        //
    // Get the mode of the module:                                            //
    //------------------------------------------------------------------------//
    L #mc.MODE;
    L "dbCONST".BLK.MODE.AUTO;
    ==I;
    = #mc.modeAUTO;

NETWORK
TITLE = Mode Manual
    L #mc.MODE;
    L "dbCONST".BLK.MODE.MANUAL;
    ==I;
    = #mc.modeMANUAL;

NETWORK
TITLE = Mode Out Of Service
    L #mc.MODE;
    L "dbCONST".BLK.MODE.OOS;
    ==I;
    = #mc.modeOOS;

NETWORK
TITLE = Interlock Flag
    //------------------------------------------------------------------------//
    // Set boolean interlock flag if any interlock condition exists:          //
    // Instance Specific Non-Critical Interlocks can be bypassed by Manual    //
    // Override command from Operator Station (HMI):                          //
    //------------------------------------------------------------------------//
    O #mc.CRIL;
    O(;
    A #mc.NCRIL;
    AN #mc.MAN_OVERRIDE;
    );
    = #mc.INTERLOCK;

NETWORK
TITLE = Send safe command in case of any interlock or failure
    //------------------------------------------------------------------------//
    // Send safe command if any interlock:                                    //
    //------------------------------------------------------------------------//
    A #mc.INTERLOCK;
    JCN _b;
    L #mc.CMD_SAFE;
    T #mc.CMD;
_b:   NOP 0;

NETWORK
TITLE = Set the safe state in case of any interlock or failure
    //------------------------------------------------------------------------//
    // Set safe state to match command if any interlock. The safe command     //
    // must match the safe state index number in dbCONST.                     //
    //------------------------------------------------------------------------//
    A #mc.INTERLOCK;
    JCN _c;
    L #mc.CMD_SAFE;
    T #mc.STATE;
_c:   NOP 0;
END_FUNCTION
