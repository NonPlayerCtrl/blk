//----------------------------------------------------------------------------//
//            Copyright 2018 Rieckermann Engineering Operations               //
//----------------------------------------------------------------------------//
// Class:       fbZSC1                                                        //
// Description:                                                               //
// Manway closed Indication and Alarm.                                        //
//----------------------------------------------------------------------------//
// Revision history:                                                          //
// Rev By               Date        CC        Note                            //
// 2.0 David Paspa      30-Jan-2018 NA        Reboot for S7-1500.             //
// 0.5 Khairul Basar    02-Oct-2009 CC-09/016 NW4/5 DI changed from NC to NO. //
// 0.4 Mr. Khoon        01-Oct-2009 CC-09/016 Mode=Initialized,OOS coded.     //
// 0.3 Khairul Basar    30-Sep-2009           Mode=3 ignored Alarm.           //
// 0.2 Khairul Basar    04-Sep-2009 CC-09/016 Mode=1 initialized, ALARM       //
//                                            renamed as ZSCA.                //
// 0.1 Khairul Basar    03-Aug-2009           Initial design.                 //
//----------------------------------------------------------------------------//


// DISABLE ALARM IF OPEN



FUNCTION_BLOCK "fbZSC1"
TITLE = ZSC1 : Manway closed Indication and Alarm
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : REO
NAME : fbZSC1
VERSION : 2.0

    VAR_INPUT
        CMD : Int := -1;     // Safe command
        DI_INPUT : Bool;     // Input signal from Field (0 = Alarm, 1 = OK)
        DI_USER : Bool;
        CUTOUT : Bool;       // Flag indicating if alarm is to be cutout (0= Alarm Enabled, 1= Alarm Disabled)
    END_VAR

    VAR_OUTPUT
        STATE : Int;         // Device State (1= CLOSED, 2= OPENED)
        INTERLOCK : Bool;    // Interlock Flag (0 = Inactive, 1 = Active)
        ZSCA : Bool;         // Alarm Flag (0 = OK Cover Closed, 1 = ALARM)
        DI_PV : Bool;        // Processed DI
    END_VAR

    VAR_IN_OUT
        MODE : Int := 1;     // Control Mode, Auto by default
        OWNER : Int;         // Value to be set or reset by Parent
    END_VAR

    VAR_TEMP
        DI_ALM_B_1 : Bool;   // Temporary processed DI signal
        modeAuto : Bool;
        modeManual : Bool;
        modeOOS : Bool;
    END_VAR

BEGIN
NETWORK
TITLE = Mode auto
      L #MODE;
      L "dbCONST".BLK.MODE.AUTO;
      ==I;
      = #modeAuto;
NETWORK
TITLE = Mode Manual
      L #MODE;
      L "dbCONST".BLK.MODE.MANUAL;
      ==I;
      = #modeManual;
NETWORK
TITLE = Out of Service Mode=3
//Out of service
      L #MODE;
      L "dbCONST".BLK.MODE.OOS;
      ==I;
      = #modeOOS;
NETWORK
TITLE = Process Digital Input Signal
      A(;
      A #DI_INPUT;
      A #modeAuto;
      O;
      A #DI_USER;
      A #modeManual;
      );
      = #DI_ALM_B_1;
      A #DI_ALM_B_1;
      = #DI_PV;
NETWORK
TITLE = Setting ALARM Flag
//When the alarm cutout flag is set the alarm message shall not be raised.
//
      AN #DI_ALM_B_1;
      AN #CUTOUT;
      AN #modeOOS;
      = #ZSCA;
NETWORK
TITLE = Setting INTERLOCK Flag
      A #DI_ALM_B_1;
      = #INTERLOCK;
NETWORK
TITLE = Setting Device State to CLOSED
      A(;
      A #DI_INPUT;
      A #modeAuto;
      O;
      A #DI_USER;
      A #modeManual;
      );
      JNB Label_0;
      L "dbCONST".BLK.ZSC1.STATE.CLOSED;
      T #STATE;
Label_0:      NOP 0;
NETWORK
TITLE = Setting Device State to OPENED
      A(;
      AN #DI_INPUT;
      A #modeAuto;
      O;
      AN #DI_USER;
      A #modeManual;
      );
      JNB Label_1;
      L "dbCONST".BLK.ZSC1.STATE.OPENED;
      T #STATE;
Label_1:      NOP 0;
END_FUNCTION_BLOCK
