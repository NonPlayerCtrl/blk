//----------------------------------------------------------------------------//
//            Copyright 2018 Rieckermann Engineering Operations               //
//----------------------------------------------------------------------------//
// Class:       fbTC1                                                         //
// Description:                                                               //
// Cooling only temperature controller.                                       //
//----------------------------------------------------------------------------//
// Revision history:                                                          //
// Rev By               Date        CC        Note                            //
// 2.0 David Paspa      24-Apr-2018 NA        Reboot for S7-1500.             //
// 0.7 Gerald Kontriner 09-Oct-2009 CC-09/016 MON_MISMATCH Logic.             //
// 0.6 Khairul Basar    06-Oct-2009 CC-09/016 PID HLM,LLM reversed for cool.  //
// 0.5 Khairul Basar    17-Sep-2009 CC-09/016 OUT_CMD as Command to MOD1 added//
// 0.4 Khairul Basar    16-Sep-2009 CC-09/016 Duplicate Auto/man parameters   //
//                                            deleted auto/manual both now    //
//                                            using same parameters.          //
// 0.3 Khairul Basar    11-Sep-2009 CC-09/016 Manual setpoint, Non PID        //
//                                            selection parameters added &    //
//                                            programmed..                    //
// 0.2 Khairul Basar    03-Sep-2009 CC-09/016 State enable/disable code added //
//                                            NTERLOCK is changed from        //
//                                            IN_OUT to OUT & program safe    //
//                                            state for Interlock.            //
//                                            CMD_AUTO/MAN & DEV_STATE.       //
//                                            paramters are added & programed.//
// 0.1 Khairul Basar    03-Aug-2009           Initial design.                 //
//----------------------------------------------------------------------------//
FUNCTION_BLOCK "fbTC1"
TITLE = TC1 : Vesel cooling only temperature controller
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : REO
NAME : fbTC1
VERSION : 2.0

//----------------------------------------------------------------------------//
// Declare variables:                                                         //
//----------------------------------------------------------------------------//
VAR_INPUT
    OWNER : Int;             // Value to be set by Parent
    SERIAL : Int;            // Unique batch or CIPSIP serial number
    CRIL : Bool;             // Critical interlock
    NCRIL : Bool;            // Non-critical interlock
    MAN_OVERRIDE : Bool;     // Override non-critical interlocks
    SETPOINT : Real;         // Setpoint
    PV : Real;               // Measured Process Value received from control module
    MAN_PID : Bool;          // Manual non pid selection
    GAIN : Real := 1.0;      // Proportional Gain
    TI : Time := T#20S;      // Integral Time
    TD : Time := T#10S;      // Derivative Time
    CYCLE : Time := T#100MS; // PID Block Sampling Time
    P_SEL : Bool := TRUE;    // Proportional Action ON/OFF
    I_SEL : Bool := TRUE;    // Integral Action ON/OFF
    D_SEL : Bool := TRUE;    // Derivative Action ON/OFF
END_VAR

VAR_OUTPUT
    TCVPOSN : Real;          // Output (0-100%) to TCV remote setpoint
END_VAR

VAR_IN_OUT
    LMN : Real;              // PID controller output (0-100%)
    CMD : Int;               // Device Command from dbCONST
    MODE : Int;              // Control Mode
    STATE : Int;             // Device State from dbCONST
    INTERLOCK : Bool;        // Interlock Flag (0 = Inactive, 1 = Active)
END_VAR

VAR
    PID1 {OriginalPartName := 'CONT_C'; LibVersion := '1.0'} : CONT_C;
END_VAR

VAR_TEMP
    cmdEnable : Bool;
    cmdDisable : Bool;
    modeAUTO : Bool;
    modeMANUAL : Bool;
    modeOOS : Bool;
    tSetpoint : Real;
    tManual_Not_PID : Bool;
END_VAR

BEGIN
NETWORK
TITLE = Call Mode and Command handling block
    //------------------------------------------------------------------------//
    // COMMAND PROCESSOR   COMMAND PROCESSOR   COMMAND PROCESSOR   COMMAND    //
    //                                                                        //
    // Process the standard block functions for command, mode, interlock and  //
    // state:                                                                 //
    //------------------------------------------------------------------------//
    CALL "fcModeCmd"
    (
        CRIL                 := #CRIL ,
        NCRIL                := #NCRIL ,
        MAN_OVERRIDE         := #MAN_OVERRIDE ,
        CMD_SAFE             := "dbCONST".BLK.TC1.SAFE.CMD ,
        MODE                 := #MODE ,
        CMD                  := #CMD ,
        STATE                := #STATE ,
        INTERLOCK            := #INTERLOCK ,
        modeAUTO             := #modeAUTO ,
        modeMANUAL           := #modeMANUAL ,
        modeOOS              := #modeOOS
    );
    NOP 0;

NETWORK
TITLE = Device command ENABLE
    //------------------------------------------------------------------------//
    // COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND    //
    // COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND    //
    //                                                                        //
    // CMD is ENABLE:                                                         //
    //------------------------------------------------------------------------//
    AN #modeOOS;
    AN #INTERLOCK;
    L #CMD;
    L "dbCONST".BLK.TC1.CMD.ENABLE;
    ==I;
    = #cmdEnable;

NETWORK
TITLE = Device command DISABLE
    //------------------------------------------------------------------------//
    // CMD is DISABLE:                                                        //
    //------------------------------------------------------------------------//
    ON #cmdEnable;
    O(;
    L #CMD;
    L "dbCONST".BLK.TC1.CMD.DISABLE;
    ==I;
    );
    = #cmdDisable;

NETWORK
TITLE = Command should be DISABLE if not ENABLE
    //------------------------------------------------------------------------//
    // CMD to DISABLE:                                                        //
    //------------------------------------------------------------------------//
    A #cmdDisable;
    JCN _a;
    L "dbCONST".BLK.TC1.CMD.DISABLE;
    T #CMD;
_a:   NOP 0;

NETWORK
TITLE = Setpoint if Enabled
    //------------------------------------------------------------------------//
    // INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS  //
    // INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS  //
    //                                                                        //
    // Setpoint if ENABLE:                                                    //
    //------------------------------------------------------------------------//
    A #cmdEnable;
    JCN _b;
    L #SETPOINT;
    T #tSetpoint;
_b:   NOP 0;

NETWORK
TITLE = Setpoint if Disabled
    //------------------------------------------------------------------------//
    // Setpoint should be 0 if disabled:                                      //
    //------------------------------------------------------------------------//
    A #cmdDisable;
    JCN _c;
    L 0;
    T #tSetpoint;
_c:   NOP 0;

NETWORK
TITLE = PID in manual if disabled or manual flag set
    //------------------------------------------------------------------------//
    // OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS    //
    // OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS   OUTPUTS    //
    //                                                                        //
    // PID in manual mode if requested:                                       //
    //------------------------------------------------------------------------//
    O #cmdDisable;
    O(;
    A #cmdEnable;
    A #modeMANUAL;
    A #MAN_PID;
    );
    = #tManual_Not_PID;

NETWORK
TITLE = PID Controller
    //------------------------------------------------------------------------//
    // PID algorithm if enabled in AUTO mode:                                 //
    //------------------------------------------------------------------------//
    CALL #PID1
    (   COM_RST              := "flagFirstScanRestart" ,
        MAN_ON               := #tManual_Not_PID ,
        P_SEL                := #P_SEL ,
        I_SEL                := #I_SEL ,
        D_SEL                := #D_SEL ,
        CYCLE                := #CYCLE ,
        SP_INT               := #tSetpoint ,
        PV_IN                := #PV ,
        MAN                  := #tSetpoint ,
        GAIN                 := #GAIN ,
        TI                   := #TI ,
        TD                   := #TD ,
        LMN_HLM              := 100.0 ,
        LMN_LLM              := 0.0 ,
        LMN                  := #LMN
    );

NETWORK
TITLE = Output should be reverse acting
    //------------------------------------------------------------------------//
    // Change the output to be reverse acting:                                //
    //------------------------------------------------------------------------//
    L 100.0;
    L #LMN;
    -R;
    T #LMN;

NETWORK
TITLE = Output if Disabled
    //------------------------------------------------------------------------//
    // Output should be 0 if disabled:                                        //
    //------------------------------------------------------------------------//
    A #cmdDisable;
    JCN _d;
    L 0.0;
    T #LMN;
_d:   NOP 0;

NETWORK
TITLE = Transfer PID output to TCV
    //------------------------------------------------------------------------//
    // Write the PID controller output to the temperature control valve (TCV) //
    // remote position setpoint:                                              //
    //------------------------------------------------------------------------//
    L #LMN;
    T #TCVPOSN;

    //------------------------------------------------------------------------//
    // STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE  //
    // STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE  //
    //                                                                        //
    // DISABLED if commanded or any problem or OOS:                           //
    //------------------------------------------------------------------------//
    A #cmdDisable;
    JCN _e;
    L "dbCONST".BLK.TC1.STATE.DISABLED;
    T #STATE;
_e:   NOP 0;

    //------------------------------------------------------------------------//
    // ENABLED if commanded and no problem:                                   //
    //------------------------------------------------------------------------//
    A #cmdEnable;
    JCN _f;
    L "dbCONST".BLK.TC1.STATE.ENABLED;
    T #STATE;
_f:   NOP 0;
END_FUNCTION_BLOCK
