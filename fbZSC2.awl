//----------------------------------------------------------------------------//
//            Copyright 2018 Rieckermann Engineering Operations               //
//----------------------------------------------------------------------------//
// Class:       fbZSC2                                                        //
// Description:                                                               //
// Flowpath connection indication.                                            //
//----------------------------------------------------------------------------//
// Revision history:                                                          //
// Rev By               Date        CC        Note                            //
// 2.0 David Paspa      30-Jan-2018 NA        Reboot for S7-1500.             //
// 0.4 Gerald Kontriner 09-Oct-2009 CC-09/016 MON_MISMATCH Logic.             //
// 0.3 Khairul Basar    02-Oct-2009 CC-09/016 NW3 DI changed from NC to NO.   //
// 0.2 Khairul Basar    04-Sep-2009 CC-09/016 Mode=1 initialized.             //
// 0.1 Khairul Basar    03-Aug-2009           Initial design.                 //
//----------------------------------------------------------------------------//
FUNCTION_BLOCK "fbZSC2"
TITLE = ZSC2 : Flowpath connection indication
{ S7_Optimized_Access := 'FALSE' }
NAME : fbZSC2
AUTHOR : Rieckermann Engineering Operations
VERSION : 2.0

   VAR_INPUT
      MODE : Int := 2;
      DI_INPUT : Bool;   // Input signal from Field (0 = Device is OUT, 1 = Device is IN)
      DI_USER : Bool;
      REQ_STATE : Bool;   // The required state, either IN or OUT, of the flowpath device.
      ARM_FLAG : Bool;
   END_VAR

   VAR_OUTPUT
      DI_PV : Bool;   // Processed DI
      DEV_STATE : Int;   // Device State (1= IN, 2= OUT, 3= MISMATCH)
      MON_MISMATCH : Bool;
   END_VAR

   VAR_TEMP
      DI_POS_B_1 : Bool;   // Temporary processed DI signal
      mode_auto : Bool;
      mode_man : Bool;
      mode_oos : Bool;
   END_VAR


BEGIN
NETWORK
TITLE = Mode auto
      L #MODE;
      L "dbCONST_VAL".CM.MODE.AUTO;
      ==I;
      = #mode_auto;
NETWORK
TITLE = Mode man
      L #MODE;
      L "dbCONST_VAL".CM.MODE.MANUAL;
      ==I;
      = #mode_man;
NETWORK
TITLE = Mode oos
      L #MODE;
      L "dbCONST_VAL".CM.MODE.OUT_OF_SERVICE;
      ==I;
      = #mode_oos;
NETWORK
TITLE = Process Digital Input Signal
      A #DI_INPUT;
      A #mode_auto;
      O;
      A #DI_USER;
      A #mode_man;
      = #DI_PV;
NETWORK
TITLE = Setting Device State to IN
      A(;
      A #DI_INPUT;
      A #mode_auto;
      O;
      A #DI_USER;
      A #mode_man;
      );
      A #REQ_STATE;
      JNB Label_0;
      L "dbCONST_VAL".CM.ZSC2_DEV_STATE.IN;
      T #DEV_STATE;
Label_0:      NOP 0;
NETWORK
TITLE = Setting Device State to OUT
      A(;
      AN #DI_INPUT;
      A #mode_auto;
      O;
      AN #DI_USER;
      A #mode_man;
      );
      AN #REQ_STATE;
      JNB Label_1;
      L "dbCONST_VAL".CM.ZSC2_DEV_STATE.OUT;
      T #DEV_STATE;
Label_1:      NOP 0;
NETWORK
TITLE = Setting Device State to MISMATCH
      A(;
      A(;
      A #DI_INPUT;
      A #mode_auto;
      O;
      A #DI_USER;
      A #mode_man;
      );
      AN #REQ_STATE;
      O;
      A(;
      AN #DI_INPUT;
      A #mode_auto;
      O;
      AN #DI_USER;
      A #mode_man;
      );
      A #REQ_STATE;
      );
      JNB Label_2;
      L "dbCONST_VAL".CM.ZSC2_DEV_STATE.MISMATCH;
      T #DEV_STATE;
Label_2:      NOP 0;
NETWORK
TITLE =
      A(;
      L #DEV_STATE;
      L "dbCONST_VAL".CM.ZSC2_DEV_STATE.MISMATCH;
      ==I;
      );
      A #ARM_FLAG;
      AN #mode_oos;
      = #MON_MISMATCH;
END_FUNCTION_BLOCK
