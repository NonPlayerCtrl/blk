//----------------------------------------------------------------------------//
//            Copyright 2018 Rieckermann Engineering Operations               //
//----------------------------------------------------------------------------//
// Class:       fbAL1                                                         //
// Description:                                                               //
// Load Cell mass indicator.                                                  //
//----------------------------------------------------------------------------//
// Revision history:                                                          //
// Rev By               Date        CC        Note                            //
// 2.0 David Paspa      19-Apr-2018 NA        Reboot for S7-1500 consolidated //
//                                            into fbAL1 from all analog      //
//                                            classes.                        //
// 0.3 Mr. Khoon        01-Oct-2009 CC-09/016 Mode=Initialized,OOS coded.     //
// 0.2 Khairul Basar    31-Aug-2009 CC-09/016 Upper/Lower range hardcoded.    //
//                                            USER_INPUT added for manual     //
//                                            mode simulation.                //
// 0.1 Khairul Basar    03-Aug-2009           Initial design.                 //
//----------------------------------------------------------------------------//
FUNCTION_BLOCK "fbAL1"
TITLE = AL1 : Generic analog indicator
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : REO
NAME : fbAL1
VERSION : 2.0

//----------------------------------------------------------------------------//
// Declare variables:                                                         //
//----------------------------------------------------------------------------//
VAR_INPUT
    CMD_ENABLE : Int;        // The enable command for the block
    STATE_ENABLED : Int;     // The enabled state of the block
    STATE_FAULT : Int;       // The fault state of the block
END_VAR

VAR_OUTPUT
    _PV : Real;              // The measured process value
    DQ : Bool;               // The quality of the measurement
    ALARM_LL : Bool;         // Low low alarm is active if true
    ALARM_L : Bool;          // Low alarm is active if true
    ALARM_H : Bool;          // High alarm is active if true
    ALARM_HH : Bool;         // High high alarm is active if true
END_VAR

VAR
    mc : "udtModeCommand";   // Block mode and command
    ai : "udtAnalog";        // Analog input structure
END_VAR

BEGIN
NETWORK
TITLE = Call Mode and Command handling function
    //------------------------------------------------------------------------//
    // MODE AND COMMAND   MODE AND COMMAND   MODE AND COMMAND   MODE AND COMM //
    // MODE AND COMMAND   MODE AND COMMAND   MODE AND COMMAND   MODE AND COMM //
    //                                                                        //
    // Process the standard block functions for command, mode, interlock and  //
    // state:                                                                 //
    //------------------------------------------------------------------------//
    CALL "fcModeCmd"
    (   mc                   := #mc
    );

NETWORK
TITLE = Call Analog Input handling function
    //------------------------------------------------------------------------//
    // ANALOG VALUE   ANALOG VALUE   ANALOG VALUE   ANALOG VALUE   ANALOG VAL //
    // ANALOG VALUE   ANALOG VALUE   ANALOG VALUE   ANALOG VALUE   ANALOG VAL //
    //                                                                        //
    // Scale the analog input value and check it is healty:                   //
    //------------------------------------------------------------------------//
    CALL "fcAnalog"
    (   ai                   := #ai
    );

NETWORK
TITLE = Set Device command which can only be ENABLE.
    //------------------------------------------------------------------------//
    // COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND    //
    // COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND    //
    //                                                                        //
    // CMD is always ENABLE:                                                  //
    //------------------------------------------------------------------------//
    L #CMD_ENABLE;
    T #mc.CMD;

NETWORK
TITLE = Promote the analog input attributes
    //------------------------------------------------------------------------//
    // Promote the analog input attributes to module level:                   //
    //------------------------------------------------------------------------//
    L #ai.PV;
    T #_PV;
    A #ai.DQ;
    = #DQ;
    A #ai.ALARM_LL;
    = #ALARM_LL;
    A #ai.ALARM_L;
    = #ALARM_L;
    A #ai.ALARM_H;
    = #ALARM_H;
    A #ai.ALARM_HH;
    = #ALARM_HH;

NETWORK
TITLE = Set INTERLOCK Flag
    //------------------------------------------------------------------------//
    // INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK  //
    // INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK  //
    //                                                                        //
    // Set interlock if any external or any problem with the measurement:     //
    //------------------------------------------------------------------------//
    O #mc.INTERLOCK;
    O #ai.ALARM_HH;
    ON #ai.DQ;
    = #mc.INTERLOCK;

NETWORK
TITLE = Set Device State to Enabled if healthy
    //------------------------------------------------------------------------//
    // STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE  //
    // STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE  //
    //                                                                        //
    // ENABLED if input signal is healthy:                                    //
    //------------------------------------------------------------------------//
    A #ai.DQ;
    JCN _a;
    L #STATE_ENABLED;
    T #mc.STATE;
_a:   NOP 0;

NETWORK
TITLE = Set Device State to Fault if any field signal error
    //------------------------------------------------------------------------//
    // FAULT if any input error:                                              //
    //------------------------------------------------------------------------//
    AN #ai.DQ;
    AN #mc.modeOOS;
    JCN _b;
    L #STATE_FAULT;
    T #mc.STATE;
_b: NOP 0;
END_FUNCTION_BLOCK
