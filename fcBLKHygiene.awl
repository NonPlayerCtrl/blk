//----------------------------------------------------------------------------//
//            Copyright 2018 Rieckermann Engineering Operations               //
//----------------------------------------------------------------------------//
// Description: Generic block hygiene status calculation.                     //
//----------------------------------------------------------------------------//
// Revision history:                                                          //
// Rev By               Date        CC        Note                            //
// 2.0 David Paspa      09-Feb-2018 NA        Reboot for S7-1500.             //
// 0.1 Khairul Basar    03-Aug-2009           Initial design.                 //
//----------------------------------------------------------------------------//
FUNCTION "fcBLKHygiene" : Void
TITLE = Generic block Hygiene Status monitoring function
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : REO
NAME : fcBLKHygiene
VERSION : 2.0

//----------------------------------------------------------------------------//
// Calling parameters should be retentive in nature:                          //
//----------------------------------------------------------------------------//
   VAR_INPUT
      CLEAN_TIME : DInt;   // 604800000
      STERILE_TIME : DInt;   // 604800000
   END_VAR

   VAR_OUTPUT
      CLEAN_TOUT : Bool;   // LAST STATUS
      STERILE_TOUT : Bool;   // LAST STATUS
   END_VAR

   VAR_IN_OUT
      HYGIENE_STATE : Int;
      CLEAN_ELAPSED_TIME : DInt;
      STERILE_ELAPSED_TIME : DInt;
   END_VAR

   VAR_TEMP
      clean_timeout : Bool;
      sterile_timeout : Bool;
      start_sterile_tmr : Bool;
      start_clean_tmr : Bool;
      reset_clean_tmr : Bool;
      reset_sterile_tmr : Bool;
   END_VAR


BEGIN
NETWORK
TITLE = Start cleanliness monitoring timer if Hygiene Status is CLEAN
      L #HYGIENE_STATE;
      L "dbCONST".BLK.HYGIENE.CLEAN;
      ==I;
      = #start_clean_tmr;
      R #CLEAN_TOUT;

NETWORK
TITLE = Start sterile monitoring timer if Hygiene Status is STERILE
      L #HYGIENE_STATE;
      L "dbCONST".BLK.HYGIENE.STERILE;
      ==I;
      = #start_sterile_tmr;
      R #STERILE_TOUT;

NETWORK
TITLE = Reset cleanliness timer if Hygiene Status is not CLEAN
      L #HYGIENE_STATE;
      L "dbCONST".BLK.HYGIENE.CLEAN;
      <>I;
      = #reset_clean_tmr;

NETWORK
TITLE = Reset sterile timer if Hygiene Status is not STERILE
      L #HYGIENE_STATE;
      L "dbCONST".BLK.HYGIENE.STERILE;
      <>I;
      = #reset_sterile_tmr;
NETWORK

TITLE = Retentive timer for CLEAN
      CALL TONR_X
      (  TMR_EN                      := #start_clean_tmr ,
         RESET                       := #reset_clean_tmr ,
         PV                          := #CLEAN_TIME ,
         DELTA_T                     := "dbCONST".CHRONO.OB1_prev_cycle ,
         Q                           := #clean_timeout ,
         ET                          := #CLEAN_ELAPSED_TIME
      );
      NOP 0;

NETWORK
TITLE = Retentive timer for STERILE
      CALL TONR_X
      (  TMR_EN                      := #start_sterile_tmr ,
         RESET                       := #reset_sterile_tmr ,
         PV                          := #STERILE_TIME ,
         DELTA_T                     := "dbCONST".CHRONO.OB1_prev_cycle ,
         Q                           := #sterile_timeout ,
         ET                          := #STERILE_ELAPSED_TIME
      );
      NOP 0;

NETWORK
TITLE = Set the Hygiene Status to USED if cleanliness timer expired
      A #clean_timeout;
      JNB Label_0;
      L "dbCONST".BLK.HYGIENE.USED;
      T #HYGIENE_STATE;
      SET;
      SAVE;
      CLR;
Label_0:      A BR;
      S #CLEAN_TOUT;

NETWORK
TITLE = Set the Hygiene Status to CLEAN if sterile timer expired
      A #sterile_timeout;
      JNB Label_1;
      L "dbCONST".BLK.HYGIENE.CLEAN;
      T #HYGIENE_STATE;
      SET;
      SAVE;
      CLR;
Label_1:      A BR;
      S #STERILE_TOUT;
END_FUNCTION
