//----------------------------------------------------------------------------//
//            Copyright 2018 Rieckermann Engineering Operations               //
//----------------------------------------------------------------------------//
// Class:       fbDI2                                                         //
// Description:                                                               //
// Normally OFF general alarm single digital input.                           //
//----------------------------------------------------------------------------//
// Revision history:                                                          //
// Rev By               Date        CC        Note                            //
// 2.0 David Paspa      30-Jan-2018 NA        Reboot for S7-1500.             //
// 0.5 Khairul Basar    02-Oct-2009 CC-09/016 NW4 DI changed from NC to NO.   //
// 0.4 Mr. Khoon        01-Oct-2009 CC-09/016 Mode=Initialized,OOS coded.     //
// 0.3 Khairul Basar    30-Sep-2009           Mode=2 initialized and Out of   //
//                                            Service without Alarm.          //
// 0.2 Khairul Basar    31-Aug-2009 CC-09/016 Mode=1 initialized.             //
// 0.1 Khairul Basar    03-Aug-2009           Initial design.                 //
//----------------------------------------------------------------------------//
FUNCTION_BLOCK "fbDI2"
TITLE = DI2 : General alarm single digital input Normally OFF
{ S7_Optimized_Access := 'TRUE' }
NAME : fbDI2
AUTHOR : REO
VERSION : 2.0

//----------------------------------------------------------------------------//
// Declare variables:                                                         //
//----------------------------------------------------------------------------//
VAR_INPUT
    DI_Auto : Bool;          // Input signal from Field
END_VAR

VAR
    mc : "udtModeCommand";   // Block mode and command
    DI_Manual : Bool;        // User digital signal for simulating
    ALARM : Bool;            // Alarm Flag (0 = OK, 1 = ALARM)
    PV : Bool;               // Processed DI
END_VAR

VAR_TEMP
    tPV : Bool;              // Temporary processed DI signal
END_VAR

BEGIN
NETWORK
TITLE = Call Mode and Command handling function
    //------------------------------------------------------------------------//
    // MODE AND COMMAND   MODE AND COMMAND   MODE AND COMMAND   MODE AND COMM //
    // MODE AND COMMAND   MODE AND COMMAND   MODE AND COMMAND   MODE AND COMM //
    //                                                                        //
    // Process the standard block functions for command, mode, interlock and  //
    // state:                                                                 //
    //------------------------------------------------------------------------//
    CALL "fcModeCmd"
    (   mc                   := #mc
    );

NETWORK
TITLE = Set Device command which can only be ENABLE.
    //------------------------------------------------------------------------//
    // COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND    //
    // COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND   COMMAND    //
    //                                                                        //
    // CMD is always ENABLE:                                                  //
    //------------------------------------------------------------------------//
    L "dbCONST".BLK.DI2.CMD.ENABLE;
    T #mc.CMD;

NETWORK
TITLE = Process Digital Input Signal
    //------------------------------------------------------------------------//
    // INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS  //
    // INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS   INPUTS  //
    //                                                                        //
    // Calculate the process value based on the mode:                         //
    //------------------------------------------------------------------------//
    O(;
    A #mc.modeAUTO;
    A #DI_Auto;
    );
    O(;
    A #mc.modeMANUAL;
    A #DI_Manual;
    );
    = #tPV;
    = #PV;

NETWORK
TITLE = Set ALARM Flag
    //------------------------------------------------------------------------//
    // ALARM   ALARM   ALARM   ALARM   ALARM   ALARM   ALARM   ALARM   ALARM  //
    // ALARM   ALARM   ALARM   ALARM   ALARM   ALARM   ALARM   ALARM   ALARM  //
    //                                                                        //
    // Set alarms:                                                            //
    //------------------------------------------------------------------------//
    A #tPV;
    AN #mc.modeOOS;
    = #ALARM;

NETWORK
TITLE = Set INTERLOCK Flag
    //------------------------------------------------------------------------//
    // INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK  //
    // INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK   INTERLOCK  //
    //                                                                        //
    // Set interlocks:                                                        //
    //------------------------------------------------------------------------//
    A #tPV;
    = #mc.INTERLOCK;

NETWORK
TITLE = Set Device State to Enabled if switch is off
    //------------------------------------------------------------------------//
    // STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE  //
    // STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE   STATE  //
    //                                                                        //
    // ENABLED if switch is off:                                              //
    //------------------------------------------------------------------------//
    AN #tPV;
    JCN _a;
    L "dbCONST".BLK.DI2.STATE.ENABLED;
    T #mc.STATE;
_a:   NOP 0;

NETWORK
TITLE = Setting Device State to ALARM
    //------------------------------------------------------------------------//
    // ALARM if switch is on and not OOS:                                     //
    //------------------------------------------------------------------------//
    A #tPV;
    AN #mc.modeOOS;
    JCN _b;
    L "dbCONST".BLK.DI2.STATE.ALARM;
    T #mc.STATE;
_b:   NOP 0;
END_FUNCTION_BLOCK
