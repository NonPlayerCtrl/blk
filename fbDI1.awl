FUNCTION_BLOCK "fbDI1"
TITLE = DI1 - General alarm single digital input
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : Synertec
NAME : fbDI1
VERSION : 0.5
////------------------------------------------------------------------------------
////                   Copyright 2009 Synertec Asia (M) Sdn Bhd 
////------------------------------------------------------------------------------
//// Title: DI1
//// 
//// Description:
//// General alarm single digital input
////------------------------------------------------------------------------------
//// Revision History:
//// Rev   By                  Date             Note
//// 0.5 Khairul Basar    02-Oct-2009 CC-09/016 NW4 DI changed from NC to NO
//// 0.4 Mr. Khoon        01-Oct-2009 CC-09/016 Mode=Initialized,OOS coded.
//// 0.3 Khairul Basar    30-Sep-2009           Mode=2 initialized and Out of 
////                                             Service without Alarm
//// 0.2 Khairul Basar    31-Aug-2009 CC-09/016  Mode=1 initialized
//// 0.1 Khairul Basar    03-Aug-2009            Initial design
////------------------------------------------------------------------------------
//
   VAR_INPUT 
      MODE : Int := 2;
      DI_INPUT : Bool;   // Input signal from Field (0 = Alarm, 1 = OK)
      DI_USER : Bool;   // User digital signal for simulating
   END_VAR

   VAR_OUTPUT 
      INTERLOCK : Bool;   // Interlock Flag (0 = Inactive, 1 = Active)
      ALARM : Bool;   // Alarm Flag (0 = OK, 1 = ALARM)
      DI_PV : Bool;   // Processed DI
      DEV_STATE : Int;   // Device State (1= OK, 2= ALARM)
   END_VAR

   VAR_TEMP 
      DI_ALM_B_1 : Bool;   // Temporary processed DI signal
      Mode_auto : Bool;
      Mode_man : Bool;
      OUTOFSERV : Bool;
   END_VAR


BEGIN
NETWORK
TITLE = Mode Auto 
      L #MODE;
      L "dbCONST_VAL".CM.MODE.AUTO;
      ==I;
      = #Mode_auto;
NETWORK
TITLE = Mode manual
      L #MODE;
      L "dbCONST_VAL".CM.MODE.MANUAL;
      ==I;
      = #Mode_man;
NETWORK
TITLE = Mode Out Of Service
      L #MODE;
      L "dbCONST_VAL".CM.MODE.OUT_OF_SERVICE;
      ==I;
      = #OUTOFSERV;
NETWORK
TITLE = Process Digital Input Signal
      A(;
      A #DI_INPUT;
      A #Mode_auto;
      O;
      A #DI_USER;
      A #Mode_man;
      );
      = #DI_ALM_B_1;
      A #DI_ALM_B_1;
      = #DI_PV;
NETWORK
TITLE = Setting ALARM Flag
      AN #DI_ALM_B_1;
      AN #OUTOFSERV;
      = #ALARM;
NETWORK
TITLE = Setting INTERLOCK Flag
      A #DI_ALM_B_1;
      = #INTERLOCK;
NETWORK
TITLE = Setting Device State to OK
      A(;
      A #DI_INPUT;
      A #Mode_auto;
      O;
      A #DI_USER;
      A #Mode_man;
      );
      JNB Label_0;
      L "dbCONST_VAL".CM.DI1_DEV_STATE.OK;
      T #DEV_STATE;
Label_0:      NOP 0;
NETWORK
TITLE = Setting Device State to ALARM
      A(;
      AN #DI_INPUT;
      A #Mode_auto;
      O;
      AN #DI_USER;
      A #Mode_man;
      );
      JNB Label_1;
      L "dbCONST_VAL".CM.DI1_DEV_STATE.ALARM;
      T #DEV_STATE;
Label_1:      NOP 0;
END_FUNCTION_BLOCK

